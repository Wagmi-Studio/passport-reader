apply plugin: 'com.android.library'

android {
    compileSdkVersion 33
    buildToolsVersion "25.0.1"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        multiDexEnabled = true
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }
    lintOptions {
       warning 'InvalidPackage'
    }
}

dependencies {
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:appcompat-v7:25.1.0'
    implementation 'org.jmrtd:jmrtd:0.5.5'
    implementation 'net.sf.scuba:scuba-sc-android:0.0.9'
    implementation 'com.madgag.spongycastle:prov:1.54.0.0'
    implementation 'edu.ucar:jj2000:5.2'
    implementation 'com.github.mhshams:jnbis:1.1.0'
    implementation 'com.facebook.react:react-native:+'
    implementation 'com.google.code.gson:gson:2.8.9'
}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

cargo {
    module  = "../../../ark-circom-passport" 
    libname = "ark_circom_passport" 
    targets =  ["arm64", "x86", "arm", "x86_64", "darwin-aarch64"]
    targetIncludes = ["libark_circom_passport.so"]
    apiLevel = 29
    profile = 'release'
}


tasks.whenTaskAdded { task ->
    if ((task.name == 'javaPreCompileDebug' || task.name == 'javaPreCompileRelease')) {
        task.dependsOn 'cargoBuild'
    }
}

// Fix the UnsatisfiedLinkError (c.f. https://github.com/mozilla/rust-android-gradle/issues/118#issuecomment-1777952460)
tasks.matching { it.name.matches(/merge.*JniLibFolders/) }.configureEach {
    it.inputs.dir(new File(buildDir, "rustJniLibs/android"))
    it.dependsOn("cargoBuild")
}
